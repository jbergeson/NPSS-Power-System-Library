// ****************************************************************************
// * NASA Glenn Research Center
// * 21000 Brookpark Rd
// * Cleveland, OH 44135
// * Jeffrey Csank, David Sadey, Tom Lavelle
// * March 18, 2019
// ****************************************************************************

class Rectifier extends Element {
	
	//------------------------------------------------------------
	//     ******* DOCUMENTATION *******
	//------------------------------------------------------------
	
	title = "";
	
	description = isA() + " Determines power for a Rectifier";
	
	usageNotes = isA() + "- NOTE TO USERS: This file....";
	
	background = "";
	
	//------------------------------------------------------------
	//     ******* SETUP VARIABLES ********
	//------------------------------------------------------------
		
	real Eff {
		value = 1.0; IOstatus=INPUT; units= NONE;
		description = "Efficiency of the rectifier";
	}

	real effBase {
		value = 1.;  IOstatus = OUTPUT;  units = NONE;
		description = "Adiabatic efficiency.  Calculated and set by S_map socket during off design.";
	}

	real effDes {
		value = 1.;  IOstatus = INPUT;  units = NONE;
		description = "Adiabatic efficiency at design point.";
	}
	
	real frequency {
		value = 60.; IOstatus=INPUT;  units = HERTZ;
		description = "Frequency of AC output power";
	}

	real Iac{
		value = 1000.;  IOstatus = OUTPUT;  units = AMP;
		description = "This is the real part of the current on the AC (output) side of the inverter, and iti is varied by the solver";
	}

	real Idc{
		value = 1000.;  IOstatus = OUTPUT;  units = AMP;
		description = "This is the current on the DC (input) side of the inverter, and it is varied by the solver";
	}

	real Iimag {
		value = 1.;  IOstatus = OUTPUT;  units = NONE;
		description = "Solver independent to vary imaginary portion of input current";
	}

	real mod_factor{
		value = 1.;  IOstatus = INPUT;  units = NONE;
		description = "This is the modulation factor that relates the magnitude of the AC current to the magnitude of the DC current";
	}

	real pwrDes {
		value = 1.;  IOstatus = INPUT;  units = KW;
		description = "Output power at design";
	}

	real pwrOut {
		value = 1.;  IOstatus = OUTPUT;  units = KW;
		description = "Output power";
	}

	real Vimag {
		value = -1.; IOstatus=INPUT;  units = "V";
		description = "Rectifier Voltage (imaginary)";
	}
	
	real Vreal {
		value = 1000.; IOstatus=INPUT;  units = "V";
		description = "Rectifier Voltage (real)";
	}
	
	//------------------------------------------------------------
    //   ******* OPTION VARIABLE SETUP *******
    //------------------------------------------------------------

    Option switchDes {
		allowedValues = { "DESIGN", "OFFDESIGN" } ;
		description = "Determines if the element is in design or off-design mode";
		rewritableValues = FALSE;
	}

	//------------------------------------------------------------
	// ****** SETUP PORTS, FLOW STATIONS, SOCKETS, TABLES ********
	//------------------------------------------------------------

	// FLUID PORTS

    // FUEL PORTS

    // BLEED PORTS
     
    // THERMAL PORTS

    //ELECTRICAL PORTS

	ElectricPort EP_O {
		description = "Electric port";
	}
	
	ElectricPort EP_I {
		description = "Electric port";
	}

	// MECHANICAL PORTS

    // FLOW STATIONS

    // SOCKETS

    Socket S_map{
		allowedValues = {"effBase"}
		description = "Socket to calculate motor map performance";
		//socketType = "INVERTER_RECTIFIER_MAP"; //this is the socketType the map should be, however, this socketType does not yet exist
		socketType = "TURBINE_EFFICIENCY_MAP";
	}

    // TABLES

	//------------------------------------------------------------
	//  ******  ADD SOLVER INDEPENDENTS & DEPENDENTS  ******
	//------------------------------------------------------------
	
	Dependent dep_phase{
		eq_rhs="EP_I.VLN.phaseDeg";
		eq_lhs="EP_I.I.phaseDeg";
		//autoSetup=TRUE;
		description = "Forces the phase angle on the voltage and current to be the same";
	}

	Independent imag_current{
		varName = "Iimag";
		description = "Varies imaginary current to match phase angle";
		//autoSetup = TRUE;
	}

	Independent dc_current{
		varName = "Idc";
		description = "Varies the EP_O current so Pout = Pin / Eff";
		autoSetup = TRUE;
	}

	Dependent input_pwr{
		eq_lhs = "EP_O.S.r / Eff";
		eq_rhs = "EP_I.S.r";
		description = "Forces input power to match output power and efficiency";
		autoSetup = TRUE;
	}

	Independent ac_current{
		varName = "Iac";
		description = "Varies real portion of AC current to match input bus voltage";
		autoSetup = TRUE;
	}

	Dependent bus_voltage{
		eq_lhs = "EP_I.V.mag * mod_factor";
		eq_rhs = "EP_O.V.r";
		autoSetup = TRUE;
		description = "Forces the AC and DC voltages to be related by their modulation factor";
	}

	//------------------------------------------------------------
	// ******* VARIABLE CHANGED METHODOLOGY *******
	//------------------------------------------------------------

	void variableChanged(string name, any oldVal){
		if(name == "switchDes"){
			if(switchDes == OFFDESIGN){
				effDes.IOstatus = "output";
			}
		}
	} // end variableChanged()

	//------------------------------------------------------------
	//   ******* PERFORM ENGINEERING CALCULATIONS *******
	//------------------------------------------------------------

	void calculate() {

		EP_O.frequency = 0.;
		EP_O.setIVRMS(Idc,EP_O.I.j,EP_O.V.r,EP_O.V.j);
		EP_I.setIVRMS(Iac,EP_I.I.j,EP_I.V.r,EP_I.V.j);
		
		pwrOut = EP_O.S.r;
		
		if (switchDes == "DESIGN" ){
			/****************************
			On-design
			****************************/			
		}	
		else{
			//*****************************
			// Off-Design
			//*****************************
		}	

		//set efficiency value
		if(!S_map.isEmpty()){
			S_map.execute();
		}
		else{
			effBase = effDes;
		}
		Eff = effBase;
	}
}


